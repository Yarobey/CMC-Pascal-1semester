{Псевдокомментарии и комментарии - не удалять при сдаче программы!}
{$mode TP}
{$R+,B+,X-}
{$codepage UTF-8}      
//=====================================================================
// ЗАДАЧА 2 (вариант 2) 20 очков (за подсказку - минус 5 очков) 
//=====================================================================
(*
Дана последовательность из натуральных чисел, конец последовательности
– ноль.  Программа в цикле  вводит эти числа (числовой ввод) и для 
каждого введённого числа определяет,  есть ли в его десятичной записи
хотя бы две совпадающие цифры. Например, для последовательности   
12345 23413 9 0  выдать ответ в виде:
12345: false   23413: true   9: false 

Требование: описать булевскую функцию IsSame(n), которая  возвращает 
значение true,  если в десятичной  записи натурального числа  n  
(параметр n – типа integer) есть хотя бы пара одинаковых цифр.  
Решение дать строго  за один просмотр (цифр) числа (т.е. нельзя 
использовать при описании функции IsSame двойных циклов).
Способ решения любой – нерекурсивный или рекурсивный (по желанию). 

Внимание! В решении запрещено рассчитывать на то, что в используемой 
версии Фри-Паскаля константа  MaxInt имеет значение 32767 (т.е. 
конкретное значение верхней границы целочисленного диапазона  нельзя 
считать известным, а значит нельзя предполагать, что в записи числа 
будет максимум  пять цифр).  

При сдаче программы следует разместить в разделе типов следующее 
переопределение имени integer:
type integer = longint; 
(переопределение уже сделано - см. ниже раздел типов этой программы)
здесь longint – целый тип Фри-Паскаля с более широким диапазоном:
[-2147483648 ..+2147483647] 

Тест, на котором будет проверяться работа этой программы - размещен
в конце программы (после точки) - его не удалять !!!
*)
//=====================================================================

program Task_2_var_2(input,output);
type
{переопределение стандартного имени integer:}
    integer = longint;
var
    n: integer;

{здесь ваше описание булевской функции IsSame(n)}
function IsSame(n: integer): boolean;
var
    s: set of 0..9;
    flag: boolean;
begin
    s := [];
    flag := false;
    while (n > 0) and not flag do begin
        if (n mod 10) in s then
            flag := true;
        s := s + [n mod 10];
        n := n div 10;
    end;
    IsSame := flag;
end;

begin 
    {в разделе операторов этой программы - НИЧЕГО НЕ МЕНЯТЬ !!!}
    writeln('=====================');
    writeln('Task_2_var_2 (IsSame)');
    writeln('=====================');
    {здесь ваше решение}
    
    read(n);
    while n <> 0 do begin
        writeln(n, ': ', IsSame(n), '    ');
        read(n)
    end;
    writeln
end.

{
test:
1234567890 1 22 134567891 1234567899 123456700 0

answer:
1234567890: FALSE
1: FALSE
22: TRUE
134567891: TRUE
1234567899: TRUE
123456700: TRUE
}

